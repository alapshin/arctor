apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: "de.mannodermaus.android-junit5"

archivesBaseName = 'arctor'

ext {
    bintrayRepo = 'maven'
    bintrayName = archivesBaseName
    bintrayDesc = 'Android MVP library'
    bintrayLicenses = ['Apache-2.0']
    bintrayVcs = 'https://github.com/alapshin/arctor'
    bintrayWebsite = 'https://github.com/alapshin/arctor'
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName version

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions.unitTests {
        includeAndroidResources = true
        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                exceptionFormat 'full'
                showStandardStreams = true
            }
        }
    }
}


dependencies {
    compileOnly libraries.supportannotations
    implementation libraries.supportappcompat

    // JSR-330
    compileOnly libraries.jsr330

    testCompileOnly junit5.unitTestsRuntime()
    testImplementation junit5.unitTests()
    testImplementation libraries.assertj
    testImplementation libraries.mockito
    testImplementation libraries.robolectric
    testImplementation libraries.robolectricsupport
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
}

if (!rootProject.hasProperty('ciBuild')) {
    apply from: rootProject.file('gradle/bintray.gradle')
    apply from: rootProject.file('gradle/localmaven.gradle')
}
