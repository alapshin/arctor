apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.alapshin'
archivesBaseName = 'arctor'
version = '0.4.1'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName version

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        // Common code for JVM tests lives in src/jvmTestsCommon.
        test.java.srcDirs += 'src/jvmTestCommon/java'

        // Unit tests live in src/unitTest.
        test.java.srcDirs += 'src/unitTest/java'
        test.resources.srcDirs += 'src/unitTest/resources'

        // Integration tests live in src/integrationTest.
        test.java.srcDirs += 'src/integrationTest/java'

        // Unit tests for debug build type specific code live in src/debugUnitTest.
        testDebug.java.srcDirs += 'src/debugUnitTest/java'

        // Unit tests for release build type specific code live in src/releaseUnitTest.
        testRelease.java.srcDirs += 'src/releaseUnitTest/java'

        // Functional tests live in src/functionalTests.
        androidTest.java.srcDirs  += 'src/functionalTest/java'
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
            showStandardStreams = true
        }
    }
}


dependencies {
    compile libraries.supportappcompat

    provided libraries.javaxannotation
    // JSR-305
    provided libraries.jsr305
    // JSR-330
    provided libraries.jsr330
    // RxJava
    compile libraries.rxjava

    testCompile libraries.junit
    testCompile libraries.assertj
    testCompile libraries.mockito
    testCompile libraries.robolectric
}
